<p>
Another euler problem about counting the greatest recurring cycle inside a fraction.
</p>

<blockquote>
<p>
Problem 26 - <a href="http://projecteuler.net/problem=26">http://projecteuler.net/problem=26</a>
A unit fraction contains 1 in the numerator. The decimal representation of the unit fractions with denominators 2 to 10 are given:
    1/2  =         0.5
    1/3  =         0.(3)
    1/4  =         0.25
    1/5  =         0.2
    1/6  =         0.1(6)
    1/7  =         0.(142857)
    1/8  =         0.125
    1/9  =         0.(1)
    1/10 =         0.1
Where 0.1(6) means 0.166666&#x2026;, and has a 1-digit recurring cycle. It can be seen that 1/7 has a 6-digit recurring cycle.
Find the value of d &lt; 1000 for which 1/d contains the longest recurring cycle in its decimal fraction part.
</p>
</blockquote>

<p>
As usual, we will add facts just below the functions to document the function.
</p>

<div id="outline-container-sec-1" class="outline-2">
<h2 id="sec-1">take-while and 1</h2>
<div class="outline-text-2" id="text-1">
<p>
We need to be able to capture elements that satisfies the first element plus the first element which falsifies the predicate.
So for this, we will use the standard take-while and add the next element. It's a take-while and 1.
</p>

<div class="org-src-container">

<pre class="src src-clojure">(defn tw
  "'take-while pred coll' with the first element that wrong the predicate pred."
  [pred coll]
  (let [[h t] (split-with pred coll)]
    (concat h [(first t)])))

(m/fact
  (tw even? [0 2 4 6 8 9 10 11 12]) =&gt; [0 2 4 6 8 9]
  (tw even? [1 2 3 4])              =&gt; [1])
</pre>
</div>
</div>
</div>

<div id="outline-container-sec-2" class="outline-2">
<h2 id="sec-2">division</h2>
<div class="outline-text-2" id="text-2">
<p>
We compute the division as we learnt it in elementary school.
We'll use the iterate function to benefit from lazyness.
</p>

<div class="org-src-container">

<pre class="src src-clojure">(defn division
  "Compute the division the elementary school way :D (D is denominator, N numerator, R remains, Q quotient)."
  [N D]
  (-&gt;&gt; {:c? true :n N}
       (iterate
	(fn [{:keys [n]}]
	  (let [R (rem n D)
		Q (quot n D)]
	    (cond (or (= 0 R) (= R D)) {:c? false :n 0        :q Q}
		  (&lt; R D)              {:c? true  :n (* 10 R) :q Q}
		  :else                {:c? true  :n R        :q Q}))))
       (drop 1)))

(m/fact :now-we-have-a-way-to-divide-infinitely
  (-&gt;&gt; (division 1 8)
       (tw :c?)
       (map :q)) =&gt; [0 1 2 5]
  (-&gt;&gt; (division 1 20)
       (tw :c?)
       (map :q)) =&gt; [0 0 5]
  (-&gt;&gt; (division 1 7)
       (take 20)
       (map :q)) =&gt; [0 1 4 2 8 5 7 1 4 2 8 5 7 1 4 2 8 5 7 1]
  (-&gt;&gt; (division 1 7)
       (take 20)
       (map :n)) =&gt; [10 30 20 60 40 50 10 30 20 60 40 50 10 30 20 60 40 50 10 30]
  (-&gt;&gt; (division 1 13)
       (take 20)
       (map :q)) =&gt; [0 0 7 6 9 2 3 0 7 6 9 2 3 0 7 6 9 2 3 0])
</pre>
</div>
</div>
</div>
<div id="outline-container-sec-3" class="outline-2">
<h2 id="sec-3">recurring-cycle-count</h2>
<div class="outline-text-2" id="text-3">
<p>
Now I want to be able to count the recurring cycles inside a sequence.
</p>

<div class="org-src-container">

<pre class="src src-clojure">(defn recurring-cycle-count
  "Compute the size of a recurring cycle in a sequence."
  [s]
  (-&gt;&gt; {:v s :m {} :i 0}
       (iterate
	(fn [{:keys [v m i] :as cp}]
	  (let [[h &amp; t] v
		p       (m h)]
	    (if p
	      (assoc cp :r (- i p))
	      {:v t :m (assoc m h i) :i (+ 1 i)}))))
       (drop-while #(nil? (:r %)))
       first
       :r))

(m/fact
  (recurring-cycle-count [10 30 20 60 40 50 10 30 20 60 40 50 10 30 20 60 40 50 10 30]) =&gt; 6
  (recurring-cycle-count [0 1 4 2 8 5 7 1 4 2 8 5 7 1 4 2 8 5 7 1])                     =&gt; 6
  (recurring-cycle-count [0 1 2 3 4 1 2 3 4])                                           =&gt; 4
  (recurring-cycle-count [0 1 2 5 2 5 2 5])                                             =&gt; 2)
</pre>
</div>
</div>
</div>

<div id="outline-container-sec-4" class="outline-2">
<h2 id="sec-4">recurring-cycle</h2>
<div class="outline-text-2" id="text-4">
<p>
Now that We have a way to infinitely divide a number by another and a function to compute the recurring-cycle count, we can compute what we want and ensure it's ok according to the problem's facts.
</p>

<p>
For this, we can create a function which will divide the number by 1, then retrieve the sequence of rest from that division and compute the recurring cycle count from this sequence.
</p>

<div class="org-src-container">

<pre class="src src-clojure">(def recurring-cycle ^{:doc "Compute the recurring cycle from a division by 1"}
  (comp recurring-cycle-count
	(partial map :n)
	(partial division 1)))

(m/tabular
 (m/fact
   (recurring-cycle ?n) =&gt; ?r)
    ?n ?r
    2  1
    3  1
    4  1
    5  1
    6  1
    7  6
    8  1
    9  1
    10 1
    13 6)
</pre>
</div>
</div>
</div>

<div id="outline-container-sec-5" class="outline-2">
<h2 id="sec-5">Result</h2>
<div class="outline-text-2" id="text-5">
<p>
So, from 0 to 1000, we compute the recurring cycle count for each number then determine the max.
The number 983 is the number with the longest recurring-cycle 982.
</p>

<div class="org-src-container">

<pre class="src src-clojure">(defn max-recurring-cycle
  "Given a limit l, return the couple [longest-recurring-cycle number] from 1 to (l-1), which corresponds to the number for which 1/number has the longest recurring cycle."
  [l]
  (-&gt;&gt; (range 1 l)
       (map (juxt recurring-cycle identity))
       (into (sorted-map))
       last))
(m/fact
  (max-recurring-cycle 1000) =&gt; [982 983])
</pre>
</div>

<p>
Some small check on time:
</p>
<div class="org-src-container">

<pre class="src src-clojure">euler-lab.core26&gt; (time (max-recurring-cycle 1000))
"Elapsed time: 237.337448 msecs"
[982 983]
</pre>
</div>
</div>
</div>
