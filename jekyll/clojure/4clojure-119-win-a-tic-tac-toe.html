<div id="table-of-contents">
<h2>Table of Contents</h2>
<div id="text-table-of-contents">
<ul>
<li><a href="#sec-1">Correctness</a></li>
<li><a href="#sec-2">Improvments</a></li>
</ul>
</div>
</div>
<blockquote>
<p>
As in <a href="http://www.4clojure.com/problem/73">problem 73</a>, a tic-tac-toe board is represented by a two dimensional vector. X is represented by :x, O is represented by :o, and empty is represented by :e.
</p>

<p>
Create a function that accepts a game piece and board as arguments, and returns a set (possibly empty) of all valid board placements of the game piece which would result in an immediate win.
</p>

<p>
Board coordinates should be as in calls to get-in. For example, [0 1] is the topmost row, center position.
</p>
</blockquote>
<p>
problem: <a href="http://www.4clojure.com/problem/119">119</a>
</p>

<p>
I reused the algorithm <a href="http://adumont.fr/blog/4clojure-analyze-a-tic-tac-toe-problem/">from the previous exercise</a> 73 which, given a board, compute the winner of the board.
And first, I developed a version to prove the correctness of the algorithm.
</p>

<div id="outline-container-sec-1" class="outline-2">
<h2 id="sec-1">Correctness</h2>
<div class="outline-text-2" id="text-1">
<p>
My main idea to solve this is to generate all possible boards by replacing for each :e the player we want to see win.
Then computing the coordinates for which it indeed worked.
</p>

<div class="org-src-container">

<pre class="src src-clojure">(defn win?
  [b]
  (letfn [(w [[[a b c]
	       [d e f]
	       [g h i]] p] (or (= p a b c)
			       (= p d e f)
			       (= p g h i)
			       (= p a d g)
			       (= p b e h)
			       (= p c f i)
			       (= p a e i)
			       (= p c e g)))]
    (cond (w b :x) :x
	  (w b :o) :o
	  :else nil)))

(defn win
  [p b]
  (let [n (-&gt; b first count)]
    (-&gt;&gt; (for [x (range n) y (range n)] [(get-in b [x y]) [x y]])
	 (filter (fn [[e _]] (= :e e)))
	 (map (fn [[_ v]] [(win? (assoc-in b v p)) v]))
	 (filter (fn [[w _]] (= p w)))
	 (map second)
	 (into #{}))))
</pre>
</div>

<p>
Simply, I pipe these different computation steps:
</p>
<ul class="org-ul">
<li>I compute all the values for all possible coordinates
</li>
<li>filter on the value :e for each coordinates
</li>
<li>replace the empty entries by the current player to play and compute the winner of the result board
</li>
<li>filter on the winner which must be the current player
</li>
<li>retrieve only the coordinates
</li>
<li>reduce them in a set
</li>
</ul>

<p>
and voila, tests ok.
</p>
</div>
</div>

<div id="outline-container-sec-2" class="outline-2">
<h2 id="sec-2">Improvments</h2>
<div class="outline-text-2" id="text-2">
<p>
From this on, I realised that most of those steps could be done directly with the initial list comprehension for.
So here it goes:
</p>

<div class="org-src-container">

<pre class="src src-clojure">(defn w?
  [b]
  (letfn [(w [[[a b c]
	       [d e f]
	       [g h i]] p] (or (= p a b c)
			       (= p d e f)
			       (= p g h i)
			       (= p a d g)
			       (= p b e h)
			       (= p c f i)
			       (= p a e i)
			       (= p c e g)))]
    (cond (w b :x) :x
	  (w b :o) :o
	  :else nil)))

(defn win [p b]
  (let [n (-&gt; b first count)
	r (range n)]
    (set (for [x r
	       y r
	       :let [v [x y]
		     e (get-in b v)]
	       :when (and (= :e e) (= p (-&gt; b (assoc-in v p) w?)))]
	   v))))

(fact (win :x [[:o :e :e]
	       [:o :x :o]
	       [:x :x :e]]) =&gt; #{[2 2] [0 1] [0 2]})

(fact (win :x [[:x :o :o]
	       [:x :x :e]
	       [:e :o :e]]) =&gt; #{[2 2] [1 2] [2 0]})

(fact (win :x [[:x :e :x]
	       [:o :x :o]
	       [:e :o :e]]) =&gt; #{[2 2] [0 1] [2 0]})

(fact (win :x [[:x :x :o]
	       [:e :e :e]
	       [:e :e :e]]) =&gt; #{})

(fact (win :o [[:x :x :o]
	       [:o :e :o]
	       [:x :e :e]]) =&gt; #{[2 2] [1 1]})
</pre>
</div>

<p>
source: <a href="https://github.com/ardumont/my-4clojure-lab/blob/master/src/my_4clojure_lab/149/core119.clj">119</a>
</p>
</div>
</div>
